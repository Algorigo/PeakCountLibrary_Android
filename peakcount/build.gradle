plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
}

def versionStr = '1.0.0'

def getCurrentGitBranch() {
    def gitBranch = "Unknown branch"
    try {
        def workingDir = new File("${project.projectDir}")
        def result = 'git rev-parse --abbrev-ref HEAD'.execute(null, workingDir)
        result.waitFor()
        if (result.exitValue() == 0) {
            gitBranch = result.text.trim()
        }
    } catch (e) {
    }
    return gitBranch
}

def getUserName() {
    return System.properties['user.name']
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

group = "com.algorigo.library"
archivesBaseName = "peakcount"
if (getCurrentGitBranch().contains('master')) {
    version = versionStr
} else {
    version = "${versionStr}-${getUserName()}-SNAPSHOT"
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = archivesBaseName
            pom {
                name = 'Peak Count Library'
                description = 'Peak Count Library for Android using Native respiration_detect_time_domain_cpp[https://github.com/Algorigo/respiration_detect_time_domain_cpp]'
                url = 'https://github.com/Algorigo/PeakCountLibrary_Android'
                artifact("$buildDir/outputs/aar/peakcount-release.aar")

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'rouddy'
                        name = 'Rouddy'
                        email = 'rouddy@naver.com'
                    }
                }

                scm {
                    connection = 'scm:https://github.com/Algorigo/PeakCountLibrary_Android.git'
                    developerConnection = 'scm:git:https://github.com/Algorigo/PeakCountLibrary_Android.git'
                    url = 'https://github.com/Algorigo/PeakCountLibrary_Android'
                }
            }
        }
    }
    repositories {
        maven {
            url = version.endsWith('SNAPSHOT') ? NEXUS_SNAPSHOT_REPOSITORY_URL : NEXUS_REPOSITORY_URL
            credentials {
                username = findProperty('nexusUsername')
                password = findProperty('nexusPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 22
        targetSdk 32

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        externalNativeBuild {
            cmake {
                cppFlags ""
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.18.1"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '11'
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'com.google.android.material:material:1.7.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}